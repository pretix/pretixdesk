group 'eu.pretix.pretixscan'
version = '0.3.3'
version version

buildscript {
    ext.kotlin_version = '1.3.20'

    repositories {
        jcenter()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'edu.sc.seis.gradle:launch4j:2.4.2'
        classpath "net.ltgt.gradle:gradle-apt-plugin:0.17"
        classpath group: 'de.dynamicfiles.projects.gradle.plugins', name: 'javafx-gradle-plugin', version: '8.8.2'
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.1'
        classpath 'org.openjfx:javafx-plugin:0.0.7'
    }
}

apply plugin: 'java'
apply plugin: 'org.openjfx.javafxplugin'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'kotlin'
apply plugin: 'application'
//apply plugin: 'javafx-gradle-plugin'
apply plugin: 'edu.sc.seis.launch4j'
apply plugin: 'kotlin-kapt'
apply plugin: 'net.ltgt.apt-idea'

sourceCompatibility = 1.8
mainClassName = 'eu.pretix.pretixscan.desktop.PretixScanMain'

repositories {
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
}

applicationDefaultJvmArgs = [
        // TODO: This is a problem as it is platform-dependent
        "-p", "/usr/lib/jvm/java-11-openjdk/lib/javafx.base.jar:/usr/lib/jvm/java-11-openjdk/lib/javafx.graphics.jar:/usr/lib/jvm/java-11-openjdk/lib/javafx.controls.jar",
        // Add module. I don't get why this is necessary, but here we are
        "--add-modules", "javafx.controls",
        // Add exports
        // See https://github.com/jfoenixadmin/JFoenix/issues/889
        "--add-exports=javafx.controls/com.sun.javafx.scene.control.inputmap=ALL-UNNAMED",
        "--add-exports=javafx.graphics/com.sun.javafx.scene=ALL-UNNAMED"
]

compileKotlin {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    kotlinOptions {
        jvmTarget = "1.8"
        apiVersion = "1.3"
        languageVersion = "1.3"
    }
}
dependencies {
    implementation 'org.openjfx:javafx-base:11'
    implementation 'org.openjfx:javafx-controls:11'
    implementation 'org.openjfx:javafx-graphics:11'

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'no.tornado:tornadofx:2.0.0-SNAPSHOT'
    implementation 'org.xerial:sqlite-jdbc:3.20.1'
    implementation 'net.harawata:appdirs:1.0.0'
    implementation 'net.java.dev.jna:platform:3.5.2'
    implementation 'joda-time:joda-time:2.9.9'
    implementation "de.huxhorn.lilith:de.huxhorn.lilith.3rdparty.junique:1.0.4"
    implementation 'com.github.kittinunf.fuel:fuel:1.12.0'
    implementation 'de.jensd:fontawesomefx-materialicons:2.2.0-5'
    implementation 'com.jfoenix:jfoenix:9.0.9'
    implementation 'org.apache.pdfbox:pdfbox:2.0.13'

    // libpretixsync
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    implementation 'io.requery:requery:1.5.1'
    implementation 'io.requery:requery-kotlin:1.5.1'
    implementation 'joda-time:joda-time:2.9.9'
    implementation 'org.json:json:20090211'
    implementation 'net.sourceforge.streamsupport:streamsupport-cfuture:1.7.0'
    annotationProcessor 'javax.annotation:jsr250-api:1.0'
    annotationProcessor 'io.requery:requery-processor:1.5.1'

    // libpretixprint
    implementation 'com.github.librepdf:openpdf:1.0.5'
    implementation 'com.google.zxing:core:3.3.3'

    implementation(project(':libpretixsync')) {
        transitive = false
    }
    implementation(project(':libpretixprint')) {
        transitive = false
    }
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

javafx {
    version = "11.0.2"
    modules = ['javafx.base', 'javafx.controls', 'javafx.graphics']
}

/*jfx {
    mainClass = mainClassName
    vendor = 'pretix.eu'
    jfxMainAppJarName = "pretixscan-${version}.jar"

    // gradle jfxNative
    identifier = null
    nativeReleaseVersion = version
    needShortcut = false
    needMenu = false
    appName = "pretixscan"
}*/

shadowJar {
    baseName = project.name
    classifier = null
    version = null
}

launch4j {
    mainClassName = mainClassName
    jar = "lib/${project.tasks.shadowJar.archiveName}"
    copyConfigurable = project.tasks.shadowJar.outputs.files
    bundledJrePath = "jre"
    bundledJre64Bit = true
    version = version
    fileDescription = "pretix Check-in application"
    copyright = "pretix.eu"
    companyName = "pretix.eu"
    productName = "pretixscan"
    //splashFileName = "../../../img/splash.bmp"
    icon = "../../../img/icon_on_shape.ico"
}
